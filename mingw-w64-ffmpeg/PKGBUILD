# Maintainer : Karl-Felix Glatzer <karl.glatzer@gmx.de>

pkgname=mingw-w64-ffmpeg
pkgver=3.2.4
pkgrel=1
epoch=1
pkgdesc="Complete solution to record, convert and stream audio and video (mingw-w64)"
arch=('any')
url="http://ffmpeg.org/"
license=('GPL3')
depends=(
'mingw-w64-bzip2' 'mingw-w64-libvpx' 'mingw-w64-crt'
'mingw-w64-sdl' 'mingw-w64-x264' 'mingw-w64-zlib'
)

options=(!strip !buildflags staticlibs)
makedepends=('mingw-w64-gcc' 'mingw-w64-pkg-config' 'yasm')
source=(http://ffmpeg.org/releases/ffmpeg-${pkgver}.tar.bz2{,.asc} allow-x264.patch)
validpgpkeys=('FCF986EA15E6E293A5644F10B4322F04D67658D8')
sha256sums=('c0fa3593a2e9e96ace3c1757900094437ad96d1d6ca19f057c378b5f394496a4'
            'SKIP'
            '7c832776c7bf2eea73e1f32f7830676d6dd82bf324e24b66b534546a340ee3b6')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  for _arch in ${_architectures}; do
    mkdir -p "${srcdir}"/build-${_arch} && cd "${srcdir}"/build-${_arch}

    "${srcdir}"/ffmpeg-${pkgver}/configure \
      --prefix="/usr/${_arch}" \
      --enable-cross-compile \
      --cross-prefix="${_arch}-" \
      --target-os=mingw32 \
      --arch=${_arch%%-*} \
      --disable-debug \
      --enable-static \
      --disable-stripping \
      --disable-gpl \
      --disable-nonfree \
      --disable-libssh \
      --disable-openssl \
      --enable-avresample \
      --disable-fontconfig \
      --disable-libfreetype \
      --disable-libvpx \
      --enable-libx264 \
      --enable-zlib \
      --enable-shared \
      --disable-doc \
      --disable-encoders \
      --enable-encoder="libx264" \
      --enable-encoder="libx264rgb" \
      --enable-encoder="mjpeg" \
      --enable-encoder="aac" \
      --disable-decoders \
      --enable-decoder="jpegls" \
      --enable-decoder="jpeg2000" \
      --enable-decoder="h264" \
      --enable-decoder="mpeg4" \
      --enable-decoder="mjpeg" \
      --enable-decoder="mjpegb" \
      --enable-decoder="vp8" \
      --enable-decoder="pcm_mulaw" \
      --enable-decoder="pcm_alaw" \
      --enable-decoder="aac"

    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}"/build-${_arch}
    make DESTDIR="$pkgdir" install

    ${_arch}-strip --strip-unneeded "${pkgdir}"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "${pkgdir}"/usr/${_arch}/lib/*.a

    mv "${pkgdir}"/usr/${_arch}/bin/*.lib "${pkgdir}"/usr/${_arch}/lib/
  done
}


prepare() {
  cd "${srcdir}"/ffmpeg-${pkgver}
  patch -p1 < ../allow-x264.patch
}
# vim:set ts=2 sw=2 et:
